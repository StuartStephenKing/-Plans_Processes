#region Prolog

vNow = TIMST (NOW(), '\Y-\m-\d \h:\i:\s' );
vUser = TM1User();

vPlanStateCube = pPrefix | '_' | pPlan | '_State';
vPlanTaskDimension = pPrefix | '_' | pPlan | '_Tasks';
vPlanControlCube = pPrefix;
vModelApprovalDimension = CELLGETS ( '}Plans', pPlan , 'Dimension' );
vModelApprovalHierarchy = CELLGETS ( '}Plans', pPlan , 'Hierarchy' );
vPlanApprovalDimension = pPrefix | '_' | pPlan | '_Approval';
vPlanApprovalHierarchy = vPlanApprovalDimension;
vPlanApprovalSubset = 'Default';



# Cube and Dimension Security
CellPutS ( 'WRITE', '}CubeSecurity', pCube, '}Plans_Everyone' );
vDimIndex = 1;
vCubeDim = TABDIM( pCube, vDimIndex );
WHILE ( vCubeDim @<> '' );
    CellPutS ( 'READ', '}DimensionSecurity', vCubeDim, '}Plans_Everyone' );
    IF ( CubeExists ( '}ElementSecurity_' | vCubeDim ) = 1 & vCubeDim @<> vModelApprovalDimension );
        LogOutput ( 'INFO', 'Dimension ' | vCubeDim | ' has element security enabled' );
    ENDIF;
    vDimIndex = vDimIndex + 1;
    vCubeDim = TABDIM( pCube, vDimIndex );
END;

# SecurityOverlay
vOverlayCube = '}SecurityOverlayGlobal_' | pCube;
vOverlayMap = '';
vDimIndex = 1;
vDim = TABDIM ( pCube, vDimIndex );
WHILE ( vDim @<> '' );
    IF ( vDim @= vModelApprovalDimension );
        vOverlayMap = vOverlayMap | '1';
    ELSE;
        vOverlayMap = vOverlayMap | '0';
    ENDIF;
    IF ( vDimIndex < CubeDimensionCountGet ( pCube ) );
        vOverlayMap = vOverlayMap | ':';
    ENDIF;
    vDimIndex = vDimIndex + 1;
    vDim = TABDIM ( pCube, vDimIndex );
END;

IF ( CubeExists ( vOverlayCube ) = 0 );
    LogOutput( 'INFO', 'Generating SecurityOverlay cube for ' | pCube | ' with map ' | vOverlayMap );
    SecurityOverlayCreateGlobalDefault ( pCube, vOverlayMap );
ELSE;
    vSecurityOverlayCubeDimensionCount = CubeDimensionCountGet( vOverlayCube );
    IF ( vSecurityOverlayCubeDimensionCount = 2 );
        vSecurityOverlayCubeFirstDimension = TABDIM ( vOverlayCube, 1 );
        IF ( vSecurityOverlayCubeFirstDimension @<> vModelApprovalDimension );
            LogOutput( 'INFO', 'Deletiing current SecurityOverlay cube for ' | pCube );
            SecurityOverlayDestroyGlobalDefault( pCube );
            LogOutput( 'INFO', 'Generating SecurityOverlay cube for ' | pCube | ' with map ' | vOverlayMap );
            
            SecurityOverlayCreateGlobalDefault ( pCube, vOverlayMap );
        ENDIF;
    ENDIF;
ENDIF;

vElementIndex = 1;
vElement = ElementName( vPlanApprovalDimension, vPlanApprovalHierarchy, vElementIndex );
WHILE ( vElement @<> '' );
    vState = CellGetS( vPlanStateCube, vElement, pTask, 'State' );
    IF ( vState @= '2'  );
        LogOutput( 'INFO', 'Disabling SecurityOverlay on cube ' | pCube | ' for member ' | vElement );
        SecurityOverlayGlobalLockNode ( 0, pCube, vElement );    
    ELSE;
        LogOutput( 'INFO', 'Enabling SecurityOverlay on cube ' | pCube | ' for member ' | vElement );
        SecurityOverlayGlobalLockNode ( 1, pCube, vElement );
    ENDIF;
    vElementIndex = vElementIndex + 1;
    vElement = ElementName ( vPlanApprovalDimension, vPlanApprovalHierarchy, vElementIndex );
END;

#endregion
#region Epilog



IF ( CubeExists ( '}CubeProperties' ) = 1 );
    CellPutS ( 'REQUIREDSHARED', '}CubeProperties', pCube, 'DATARESERVATIONMODE' );
ENDIF;


#endregion